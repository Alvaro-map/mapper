#
#    Copyright 2017 Kai Pastor
#    
#    This file is part of OpenOrienteering.
# 
#    OpenOrienteering is free software: you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation, either version 3 of the License, or
#    (at your option) any later version.
# 
#    OpenOrienteering is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
# 
#    You should have received a copy of the GNU General Public License
#    along with OpenOrienteering.  If not, see <http://www.gnu.org/licenses/>.


# This qmake project file is to create a configuration for androiddeployqt.

!android:error("This project is for Android only.")

TEMPLATE = app
TARGET   = Mapper

ANDROID_PACKAGE_SOURCE_DIR = "@PROJECT_SOURCE_DIR@/android"

EXPECTED_VERSION = @Mapper_VERSION_MAJOR@\.@Mapper_VERSION_MINOR@\.@Mapper_VERSION_PATCH@
!system(grep "$$EXPECTED_VERSION" "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml") {
	error(The version name in AndroidManifest.xml does not match $$EXPECTED_VERSION.)
}

@USE_DEV_SUFFIX@ {
  # Don't let unstable packages replace official release packages.
  system(cp -a "$$ANDROID_PACKAGE_SOURCE_DIR" "$$OUT_PWD/")
  ANDROID_PACKAGE_SOURCE_DIR = $$OUT_PWD/android
  system(sed -e $$shell_quote(/<manifest/ s/org.openorienteering.mapper/org.openorienteering.mapper.dev/) -i -- "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml")
  system(sed -e $$shell_quote(/<manifest/ s/@Mapper_VERSION@/@Mapper_VERSION_DISPLAY@/) -i -- "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml")
  system(sed -e $$shell_quote(s/%%INSERT_APP_NAME%% *-->/%%INSERT_APP_NAME%% --> @Mapper_VERSION_DISPLAY@/) -i -- "$$ANDROID_PACKAGE_SOURCE_DIR/res/values/strings.xml")
  system(rm -f "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml--" "$$ANDROID_PACKAGE_SOURCE_DIR/res/values/strings.xml--")
}


versionAtLeast(QT_VERSION, 5.12) {
  system(sed -e $$shell_quote(s/minSdkVersion=\"[0-9]*\"/minSdkVersion=\"16\"/) -i -- "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml")
  system(rm -f "$$ANDROID_PACKAGE_SOURCE_DIR/AndroidManifest.xml--")
}


# For the generated .pro file to be used in Qt Creator,
# we must install everything and replace the dummy libMapper.so.
INSTALLS            += everything

everything.path  = /
everything.extra = \
  "@CMAKE_COMMAND@" --build "@PROJECT_BINARY_DIR@" && \
  "@CMAKE_COMMAND@" -E copy "$<TARGET_FILE:Mapper>" "$$OUT_PWD/" && \
  "@CMAKE_COMMAND@" -E chdir "@PROJECT_BINARY_DIR@" \
    "@CMAKE_COMMAND@" "-DCMAKE_INSTALL_PREFIX=$(INSTALL_ROOT)" -P cmake_install.cmake
